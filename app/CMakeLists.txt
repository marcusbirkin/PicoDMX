cmake_minimum_required(VERSION 3.19)

# Source
file(GLOB_RECURSE PROJECT_HEADERS CONFIGURE_DEPENDS "src/*.hpp")
file(GLOB_RECURSE PROJECT_SOURCES CONFIGURE_DEPENDS "src/*.cpp")
file(GLOB_RECURSE PROJECT_UIS CONFIGURE_DEPENDS "src/*.ui")
file(GLOB_RECURSE PROJECT_QRCS CONFIGURE_DEPENDS "res/*.qrc")
if(CMAKE_SYSTEM_NAME STREQUAL "Windows")
    set(APP_RESOURCE "res/${PROJECT_NAME}.rc")
endif()
qt_add_executable(${PROJECT_NAME}
    ${PROJECT_HEADERS}
    ${PROJECT_SOURCES}
    ${PROJECT_UIS}
    ${PROJECT_QRCS}
    ${APP_RESOURCE}
)
target_include_directories(${PROJECT_NAME} PRIVATE "src")

# Installable items
install(TARGETS ${PROJECT_NAME} COMPONENT ${PROJECT_NAME})

# Create PDB files for Release
include(mscv_pdb)

# Qt
target_link_libraries(${PROJECT_NAME} PRIVATE Qt${QT_VERSION_MAJOR}::Widgets)

# PicoSDK
include(picosdk)

# Version numbering
string(TIMESTAMP YEAR "%Y")
target_compile_definitions(${PROJECT_NAME} PRIVATE
    VER_FILEVERSION=${PROJECT_VERSION_MAJOR},${PROJECT_VERSION_MINOR},${PROJECT_VERSION_PATCH},${PROJECT_VERSION_TWEAK}
    VER_FILEVERSION_STR="${PROJECT_VERSION}"
    VER_PRODUCTVERSION=${PROJECT_VERSION}
    VER_PRODUCTVERSION_STR="${PROJECT_VERSION}"
    VER_COMPANYNAME_STR="Marcus Birkin"
    VER_FILEDESCRIPTION_STR="${PROJECT_NAME}"
    VER_INTERNALNAME_STR="${PROJECT_NAME}"
    VER_LEGALCOPYRIGHT_STR="{YEAR} Marcus Birkin"
    VER_ORIGINALFILENAME_STR="${PROJECT_NAME}.exe"
    VER_PRODUCTNAME_STR="${PROJECT_NAME}"
)

# Binary properties
if(CMAKE_SYSTEM_NAME STREQUAL "Windows")
    set_target_properties(${PROJECT_NAME} PROPERTIES WIN32_EXECUTABLE TRUE)
endif()
